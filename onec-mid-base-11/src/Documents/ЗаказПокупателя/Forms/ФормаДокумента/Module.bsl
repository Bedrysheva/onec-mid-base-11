
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры



#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	//++ Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025)
	СуммарнаяСкидка =  ТекущиеДанные.Скидка + Объект.Р1_СогласованнаяСкидка;
	Если СуммарнаяСкидка > 100 Тогда
		ТекстСообщения = СтрШаблон("Суммарная скидка превышает 100%1 в строке №%2. Сумма строки не пересчитана!", "%" , ТекущиеДанные.НомерСтроки);
		Сообщить(ТекстСообщения);	
		ТекущиеДанные.Сумма = 0;					 
		Возврат;
	КонецЕсли;
	//-- Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025)
	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//++ Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025)
	КоэффициентСкидки = 1 - СуммарнаяСкидка / 100;
	//-- Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТаблицы()
//++ Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025) 	

	Для Каждого СтрокаТЧ Из Объект.Товары Цикл 
		РассчитатьСуммуСтроки(СтрокаТЧ);  
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Объект.Услуги Цикл 
		РассчитатьСуммуСтроки(СтрокаТЧ); 
	КонецЦикла;
	
    РассчитатьСуммуДокумента();
//-- Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025) 	

КонецПроцедуры	 


#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаКлиенте
Асинх Процедура Подключаемый_СогласованнаяСкидкаПриИзменении(Элемент)
//++ Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025)	
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат;
	КонецЕСли;	
	
    ТекстВопроса = "Изменен процент скидки. Пересчитать таблицы товаров и услуг?";
	СписокЗначенийОтветов = Новый СписокЗначений;
	СписокЗначенийОтветов.Добавить(1,"Да");
	СписокЗначенийОтветов.Добавить(2,"Нет");
		
	ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, СписокЗначенийОтветов);  
		
	Если ОтветПользователя = 1 Тогда
		ПересчитатьТаблицы();
 	КонецЕсли;
//-- Бедрышева Н.В. Задача "Согласованная скидка" (09.02.2025) 		               
КонецПроцедуры

#КонецОбласти
